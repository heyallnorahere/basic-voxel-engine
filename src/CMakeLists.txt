cmake_minimum_required(VERSION 3.10)
file(GLOB_RECURSE CPP_SOURCE_FILES "${CMAKE_CURRENT_SOURCE_DIR}/lib/*.cpp")
file(GLOB_RECURSE INTERNAL_HEADER_FILES "${CMAKE_CURRENT_SOURCE_DIR}/lib/*.h")
file(GLOB_RECURSE H_HEADER_FILES "${CMAKE_CURRENT_SOURCE_DIR}/include/*.h")
set(MANIFEST ${CPP_SOURCE_FILES} ${INTERNAL_HEADER_FILES} ${H_HEADER_FILES})
set(BVE_TARGET_NAME basic-voxel-engine)
if(BVE_BUILD_EXECUTABLE)
    file(GLOB_RECURSE EXECUTABLE_SOURCE_FILES "${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp")
    file(GLOB_RECURSE EXECUTABLE_HEADER_FILES "${CMAKE_CURRENT_SOURCE_DIR}/src/*.h")
    add_executable(${BVE_TARGET_NAME} ${MANIFEST} ${EXECUTABLE_SOURCE_FILES} ${EXECUTABLE_HEADER_FILES})
else()
    add_library(${BVE_TARGET_NAME} STATIC ${MANIFEST})
endif()
set_target_properties(${BVE_TARGET_NAME} PROPERTIES CXX_STANDARD 17 FOLDER bve)
target_include_directories(${BVE_TARGET_NAME} PUBLIC "${PROJECT_SOURCE_DIR}/vendor/glm" "${PROJECT_SOURCE_DIR}/vendor/stb" "${CMAKE_CURRENT_SOURCE_DIR}/include" ${mono_HEADERS} ${Vulkan_INCLUDE_DIR})
target_link_libraries(${BVE_TARGET_NAME} PUBLIC glfw glad spdlog EnTT imgui assimp ${mono_LIBRARIES} ${Vulkan_LIBRARIES} glslang SPIRV glslang-default-resource-limits)
target_compile_definitions(${BVE_TARGET_NAME} PUBLIC "MONO_ASSEMBLIES=\"${mono_ASSEMBLIES}\"" "BVE_TARGET_NAME=\"${BVE_TARGET_NAME}\"")
if(${CMAKE_VERSION} VERSION_GREATER_EQUAL 3.16)
    target_precompile_headers(${BVE_TARGET_NAME} PRIVATE "${CMAKE_CURRENT_SOURCE_DIR}/include/bve_pch.h")
endif()
if(WIN32)
    target_compile_definitions(${BVE_TARGET_NAME} PUBLIC BVE_PLATFORM_WINDOWS)
    add_custom_command(TARGET ${BVE_TARGET_NAME} POST_BUILD COMMAND ${CMAKE_COMMAND} -E copy_if_different ${mono_DLL} $<TARGET_FILE_DIR:${BVE_TARGET_NAME}>)
elseif(UNIX)
    target_compile_definitions(${BVE_TARGET_NAME} PUBLIC BVE_PLATFORM_UNIX)
    if(APPLE)
        target_compile_definitions(${BVE_TARGET_NAME} PUBLIC BVE_PLATFORM_MACOSX)
    else()
        target_compile_definitions(${BVE_TARGET_NAME} PUBLIC BVE_PLATFORM_LINUX)
        target_link_libraries(${BVE_TARGET_NAME} PUBLIC stdc++fs pthread)
    endif()
endif()